/*
 * Copyright (c) 2017 Nike, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 * http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

repositories {
    jcenter()
}

def AWS_SDK_VERSION = '1.11.+'

configurations.all {
    resolutionStrategy {
        force "com.fasterxml.jackson.core:jackson-databind:2.11.1"
        force "com.fasterxml.jackson.dataformat:jackson-dataformat-cbor:2.11.1"
        force "ch.qos.logback:logback-core:1.2.3"
        force "ch.qos.logback:logback-classic:1.2.3"
        force "net.sourceforge.pmd:pmd-core:6.21.0"
        force "net.sourceforge.pmd:pmd-java:6.21.0"

        dependencySubstitution {
            substitute module("org.mortbay.jetty:jetty") with module("org.eclipse.jetty:jetty-server:9.4.34.v20201102")
            substitute module("org.mortbay.jetty:jetty-util") with module("org.eclipse.jetty:jetty-util:9.4.34.v20201102")
            substitute module("dom4j:dom4j:1.6.1") with module("org.dom4j:dom4j:2.1.3")
        }
    }
    exclude group: 'org.mortbay.jetty', module:'servlet-api-2.5'
}

//noinspection GroovyAssignabilityCheck
dependencies {

/***********************************************************************************************************************
 * IMPORTANT!  This project produces a fat jar!                                                                        *
 *                                                                                                                     *
 * If you add a new compile dependency to this library and do not also verify that it gets shaded                      *
 * you will introduce a transitive dependency nightmare for users of this library.                                     *
 *                                                                                                                     *
 * We do this because AWS constantly breaks backwards compatibility of their SDK with minor version releases.          *
 * We do not want to dictate what SDK version users of Cerberus should use.                                            *
 ***********************************************************************************************************************/

    // these will be added to the POM and excluded from the shadow jar
    shadow "joda-time:joda-time:2.8.1"
    shadow "org.apache.commons:commons-lang3:3.4"
    shadow "org.slf4j:slf4j-api:1.7.25"
    shadow "com.google.code.gson:gson:2.5"

    compile "com.squareup.okhttp3:okhttp:3.14.9"
    compile "org.apache.commons:commons-lang3:3.4"
    compile "com.google.code.gson:gson:2.5"
    compile "org.slf4j:slf4j-api:1.7.25"
    compile "io.github.resilience4j:resilience4j-all:1.5.0"
    compileOnly "com.google.code.findbugs:jsr305:3.0.2"
    compileOnly 'com.google.code.findbugs:annotations:3.0.1'

    compile "com.amazonaws:aws-java-sdk-core:${AWS_SDK_VERSION}"
    compile "com.amazonaws:aws-java-sdk-sts:${AWS_SDK_VERSION}"

    testRuntime 'org.slf4j:slf4j-simple:1.7.30'
    testCompile "junit:junit:4.12"
    testCompile ("org.mockito:mockito-core:1.10.19") {
        exclude group: 'org.hamcrest'
    }
    testCompile ("org.powermock:powermock-api-mockito:1.6.4") {
        exclude group: 'org.hamcrest'
    }
    testCompile ("org.powermock:powermock-module-junit4:1.6.4") {
        exclude group: 'org.hamcrest'
    }
    testCompile "org.assertj:assertj-core:2.3.0"
    testCompile "com.squareup.okhttp3:mockwebserver:3.14.9"
    testCompile "commons-io:commons-io:2.4"
    testCompile group: 'com.tngtech.java', name: 'junit-dataprovider', version: '1.10.0'
}

shadowJar {
    classifier = ''
    dependencies {
      // exclude these from the shadow jar
      project.configurations.shadow.each {
        exclude it.name
      }
    }
    relocate('com.', 'cerberus.com.') {
        exclude 'com.nike.**'
        exclude 'com.google.**'
    }
    relocate('org.', 'cerberus.org.') {
        exclude 'org.slf4j.**'
        exclude 'org.joda.**'
        exclude 'org.apache.commons.lang3.**'
        exclude 'org.w3c.dom.**'
        exclude 'org.xml.**'
    }
    relocate 'models.', 'cerberus.models.'
    relocate 'mozilla.', 'cerberus.mozilla.'
    relocate 'software.', 'cerberus.software.'
    relocate 'okio.', 'cerberus.okio.'
    relocate 'okhttp3.', 'cerberus.okhttp3.'
}

shadowJar.dependsOn copyProjectVersion
